apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    signingConfigs {
        YYBKey {
            keyAlias        project.YYB_KEY_ALIAS
            keyPassword     project.YYB_KEY_PWD
            storeFile       file(project.YYB_FILE_PATH)
            storePassword   project.YYB_STORE_PWD
        }
        KuAnKey {
            keyAlias        project.KUAN_KEY_ALIAS
            keyPassword     project.KUAN_KEY_PWD
            storeFile       file(project.KUAN_FILE_PATH)
            storePassword   project.KUAN_STORE_PWD
        }
    }

    compileSdkVersion 27
    buildToolsVersion '27.0.3'
    defaultConfig {
        applicationId "com.wxm.camerajob"
        minSdkVersion 24
        targetSdkVersion 27
        versionCode 10
        versionName "1.10"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            buildConfigField "Boolean", "ThrowDebugException", "false"
            buildConfigField "Boolean", "TestCamera", "false"
        }
        debug {
            buildConfigField "Boolean", "ThrowDebugException", "true"
            buildConfigField "Boolean", "TestCamera", "true"
        }
    }

    sourceSets {
        main {
            res.srcDirs =
                    [
                            'src/main/res',
                            'src/main/res/layout/activity',
                            'src/main/res/layout/fragment',
                            'src/main/res/layout/page',
                            'src/main/res/layout/dialog',
                            'src/main/res/layout/item',
                            'src/main/res/layout'
                    ]
        }
    }

    productFlavors {
        YYB_CameraJob{
            signingConfig signingConfigs.YYBKey
        }

        KuAn_CameraJob{
            signingConfig signingConfigs.KuAnKey
        }
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "${variant.name}-${variant.versionName}-${releaseTime()}.apk"
        }
    }

    flavorDimensions "default"
    productFlavors {
        alpha {}
    }

    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
}

static def releaseTime() {
    return new Date().format("yyyyMMdd", TimeZone.getTimeZone("GMT+8"))
}


dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    testImplementation 'junit:junit:4.12'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.jakewharton:kotterknife:0.1.0-SNAPSHOT'

    implementation files('libs/extend_libs/javamail/activation.jar')
    implementation files('libs/extend_libs/javamail/additionnal.jar')
    implementation files('libs/extend_libs/javamail/mail.jar')
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:support-v13:27.1.1'
    implementation 'org.apache.logging.log4j:log4j:2.6.1'
    implementation 'com.facebook.fresco:fresco:0.12.0'

    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'

    implementation 'org.greenrobot:eventbus:3.1.1'

    implementation 'io.reactivex:rxandroid:1.1.0'
    implementation 'com.trello:rxlifecycle:0.5.0'
    implementation 'com.trello:rxlifecycle-components:0.5.0'
    implementation 'com.squareup.retrofit2:retrofit:2.0.1'
    implementation 'com.squareup.retrofit2:converter-gson:2.0.1'
    implementation 'com.squareup.retrofit2:adapter-rxjava:2.0.1'

    implementation 'com.nostra13.universalimageloader:universal-image-loader:1.9.5'
    implementation 'org.xutils:xutils:3.3.36'
    implementation 'cn.finalteam:toolsfinal:1.1.4'
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'
    implementation 'org.jetbrains:annotations:15.0'

    implementation project(':galleryfinal')
    implementation project(':androidutil')
    implementation project(':uilib')
    implementation 'com.android.support:cardview-v7:27.1.1'
}

configurations.all {
    resolutionStrategy {
        force 'com.android.support:support-annotations:24.2.1'
    }
}

repositories {
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
}